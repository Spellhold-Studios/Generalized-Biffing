/* ====================================== *
 *         COMPONENT INSTALLATION         *
 * ====================================== */

INCLUDE ~%MOD_FOLDER%/lib/functions.tph~

/* --------------------------------------------------------- *
 *  biffing_all = 0 ==> Biff only .wav, .tis and .bam files  *
 *  biffing_all = 1 ==> Biff all files                       *
 * --------------------------------------------------------- */

ACTION_IF ~%WEIDU_OS%~ STRING_COMPARE_CASE ~WIN32~ THEN BEGIN
  AT_UNINSTALL ~rm -rf generalized_biffing/prod/0~
END ELSE BEGIN
  AT_UNINSTALL ~del /q /f generalized_biffing\prod\0~ EXACT
END

<<<<<<<< .../list-of-files
>>>>>>>>

// this defines a one to eight character regular expression for matching resrefs
// -----------------------------------------------------------------------------
OUTER_TEXT_SPRINT ~char~ ~[^ %TAB%%WNL%]~
OUTER_TEXT_SPRINT ~8char~ ~%char%%char%?%char%?%char%?%char%?%char%?%char%?%char%?~
OUTER_TEXT_SPRINT ~9char~ ~%char%%char%%char%%char%%char%%char%%char%%char%%char%%char%*~
OUTER_TEXT_SPRINT ~ext~ ~\(wav\|tis\|bam\)~
ACTION_IF biffing_all BEGIN
  ACTION_IF ENGINE_IS ~bgee bg2ee iwdee pstee~ BEGIN
    OUTER_TEXT_SPRINT ee_types ~\|fnt\|glsl\|gui\|lua\|menu\|png\|pvrz\|sql\|ttf\|wbm~
  END ELSE BEGIN
    OUTER_TEXT_SPRINT ee_types ~~
  END
  OUTER_TEXT_SPRINT ext ~\(2da\|are\|bam\|bcs\|bmp\|chr\|chu\|cre\|dlg\|eff\|gam\|ids\|ini\|itm\|mos\|mve\|plt\|pro\|spl\|sto\|tis\|vef\|vvc\|wav\|wed\|wfx\|wmp%ee_types%\)~
END

OUTER_TEXT_SPRINT ~myRegExp~ ~^%8char%\.%ext%$~

// WeiDU biffing magic
// -------------------
MKDIR ~generalized_biffing/prod/0/biffs/0~
MKDIR ~generalized_biffing/prod/0/rej~

// getting list of files that must not be biffed
LAF gb#get_blacklisted_files RET_ARRAY blacklisted blacklisted_regexp END

OUTER_SET currentTotal = 0
OUTER_SET currentFile = 0
ACTION_BASH_FOR ~override~ ~%myRegExp%~ BEGIN
  LAF gb#is_blacklisted STR_VAR filename = EVAL ~%BASH_FOR_FILE%~ RET result END
  ACTION_IF (NOT result) BEGIN
    ACTION_IF %BASH_FOR_SIZE% + currentTotal > 30000000 /* 30M */ && currentTotal > 0 BEGIN
      MAKE_BIFF ~tb#gen%currentFile%~ BEGIN ~generalized_biffing/prod/0/biffs/%currentFile%~ ~^.*$~ END
      OUTER_SET currentFile = currentFile + 1
      OUTER_SET currentTotal = 0
      MKDIR ~generalized_biffing/prod/0/biffs/%currentFile%~
    END
    MOVE ~override/%BASH_FOR_FILE%~ ~generalized_biffing/prod/0/biffs/%currentFile%/%BASH_FOR_FILE%~
    OUTER_SET currentTotal += BASH_FOR_SIZE
  END ELSE BEGIN
    PRINT @100  // Skipping file: %BASH_FOR_FILE%
  END
END

ACTION_IF currentTotal > 0 BEGIN
  MAKE_BIFF ~tb#gen%currentFile%~ BEGIN ~generalized_biffing/prod/0/biffs/%currentFile%~ ~^.*$~ END
END

OUTER_TEXT_SPRINT ~myRegExp~ ~^%9char%.%ext%$~
ACTION_BASH_FOR ~override~ ~%myRegExp%~ BEGIN
  LAF gb#is_blacklisted STR_VAR filename = EVAL ~%BASH_FOR_FILE%~ RET result END
  ACTION_IF (NOT result) BEGIN
    OUTER_PATCH_SAVE ~8name~ ~%BASH_FOR_FILE%~ BEGIN
      DELETE_BYTES 8 (INDEX_BUFFER (EXACT_MATCH ~.~) - 8)
    END
    ACTION_IF FILE_EXISTS_IN_GAME ~%8name%~ THEN BEGIN
      MOVE ~override/%BASH_FOR_FILE%~ ~generalized_biffing/prod/0/rej/%BASH_FOR_FILE%~
    END
  END
END
