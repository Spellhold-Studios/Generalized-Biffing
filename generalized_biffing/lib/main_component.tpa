/* ====================================== *
 *         COMPONENT INSTALLATION         *
 * ====================================== */

INCLUDE ~%MOD_FOLDER%/lib/functions.tph~

/* --------------------------------------------------------- *
 *  biffing_all = 0 ==> Biff only .wav, .tis and .bam files  *
 *  biffing_all = 1 ==> Biff all files                       *
 * --------------------------------------------------------- */

ACTION_IF ~%WEIDU_OS%~ STRING_COMPARE_CASE ~WIN32~ THEN BEGIN
  AT_UNINSTALL ~rm -rf generalized_biffing/prod/0~
END ELSE BEGIN
  AT_UNINSTALL ~del /q /f generalized_biffing\prod\0~ EXACT
END

// this defines a one to eight character regular expression for matching resrefs
// -----------------------------------------------------------------------------
OUTER_TEXT_SPRINT ~char~ ~[^ %TAB%%WNL%]~
OUTER_TEXT_SPRINT ~8char~ ~%char%%char%?%char%?%char%?%char%?%char%?%char%?%char%?~
OUTER_TEXT_SPRINT ~9char~ ~%char%%char%%char%%char%%char%%char%%char%%char%%char%%char%*~
OUTER_TEXT_SPRINT ~ext~ ~\(wav\|tis\|bam\)~
ACTION_IF biffing_all BEGIN
  ACTION_IF ENGINE_IS ~bgee bg2ee iwdee pstee~ BEGIN
    OUTER_TEXT_SPRINT ee_types ~\|fnt\|glsl\|gui\|lua\|menu\|png\|pvrz\|sql\|ttf\|wbm~
  END ELSE BEGIN
    OUTER_TEXT_SPRINT ee_types ~~
  END
  OUTER_TEXT_SPRINT ext ~\(2da\|are\|bam\|bcs\|bmp\|chr\|chu\|cre\|dlg\|eff\|gam\|ids\|ini\|itm\|mos\|mve\|plt\|pro\|spl\|sto\|tis\|vef\|vvc\|wav\|wed\|wfx\|wmp%ee_types%\)~
END

OUTER_TEXT_SPRINT ~myRegExp~ ~^%8char%\.%ext%$~

// WeiDU biffing magic
// -------------------
MKDIR ~generalized_biffing/prod/0/biffs/0~
MKDIR ~generalized_biffing/prod/0/rej~

// getting list of files that must not be biffed
LAF gb#get_blacklisted_files RET_ARRAY blacklisted blacklisted_regexp END

// getting list of files to biff
PRINT @101 // Preparing files for biffing...
LAF prepare_filelist
  STR_VAR
    ext_pattern  = EVAL ~%ext%~
    file_pattern = EVAL ~%myRegExp%~
  RET
    list_size
  RET_ARRAY
    file_list
    src_to_repl_file
    repl_to_dst_resref
END

// biffing files
PRINT @102 // Biffing files...
OUTER_SET currentTotal = 0
OUTER_SET currentFile = 0
OUTER_FOR (i = 0; i < list_size; ++i) BEGIN
  OUTER_SPRINT filename $file_list(~%i%~)
  OUTER_SPRINT src_file ~override/%filename%~

  OUTER_SET fileSize = SIZE_OF_FILE ~%src_file%~
  ACTION_IF fileSize + currentTotal > 30000000 /* 30M */ && currentTotal > 0 BEGIN
    MAKE_BIFF ~tb#gen%currentFile%~ BEGIN ~generalized_biffing/prod/0/biffs/%currentFile%~ ~^.*$~ END
    OUTER_SET currentFile += 1
    OUTER_SET currentTotal = 0
    MKDIR ~generalized_biffing/prod/0/biffs/%currentFile%~
  END

  ACTION_IF (VARIABLE_IS_SET $src_to_repl_file(~%filename%~)) BEGIN
    OUTER_SPRINT dst_file $src_to_repl_file(~%filename%~)
  END ELSE BEGIN
    OUTER_SPRINT dst_file ~%filename%~
  END
  OUTER_SPRINT dst_file ~generalized_biffing/prod/0/biffs/%currentFile%/%dst_file%~
  MOVE ~%src_file%~ ~%dst_file%~
  OUTER_SET currentTotal += fileSize
END

// biffing remaining files
ACTION_IF currentTotal > 0 BEGIN
  MAKE_BIFF ~tb#gen%currentFile%~ BEGIN ~generalized_biffing/prod/0/biffs/%currentFile%~ ~^.*$~ END
END

// storing rejected files
OUTER_TEXT_SPRINT ~myRegExp~ ~^%9char%.%ext%$~
ACTION_BASH_FOR ~override~ ~%myRegExp%~ BEGIN
  LAF gb#is_blacklisted STR_VAR filename = EVAL ~%BASH_FOR_FILE%~ RET result END
  ACTION_IF (NOT result) BEGIN
    OUTER_PATCH_SAVE ~8name~ ~%BASH_FOR_FILE%~ BEGIN
      DELETE_BYTES 8 (INDEX_BUFFER (EXACT_MATCH ~.~) - 8)
    END
    ACTION_IF FILE_EXISTS_IN_GAME ~%8name%~ THEN BEGIN
      MOVE ~override/%BASH_FOR_FILE%~ ~generalized_biffing/prod/0/rej/%BASH_FOR_FILE%~
    END
  END
END

// restoring original resource names in CHITIN.KEY
ACTION_IF (list_size > 0) BEGIN
  COPY ~chitin.key~ ~chitin.key~
    READ_ASCII 0 sig (8)
    PATCH_IF (~%sig%~ STR_EQ ~KEY V1  ~) BEGIN
      READ_LONG 0x0c num_entries
      READ_LONG 0x14 ofs_entries
      FOR (i = 0; i < num_entries; ++i) BEGIN
        SET cur_ofs = ofs_entries + i * 0x0e
        READ_ASCII cur_ofs resname (8) NULL
        PATCH_IF (VARIABLE_IS_SET $repl_to_dst_resref(~%resname%~)) BEGIN
          SPRINT new_resname $repl_to_dst_resref(~%resname%~)
          WRITE_ASCIIE cur_ofs ~%new_resname%~ (8)
        END
      END
    END
  BUT_ONLY
END
